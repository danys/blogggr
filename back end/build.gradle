buildscript {
    repositories {
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "http://repository.jboss.com" }
        maven { url "https://repo.spring.io/release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.0.RELEASE")
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
    id 'net.ltgt.apt' version '0.10'
    id 'org.sonarqube' version '2.6.2'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

description = 'A simple blogging application'
group = 'com.blogggr'

sourceCompatibility = 1.9
targetCompatibility = 1.9

bootJar {
    dependsOn(':frontend:copyFrontendFiles')
    baseName = project.name
    version = project.version
}

repositories {
    mavenLocal()
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url "http://repository.jboss.com" }
    maven { url "https://repo.spring.io/release" }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web:2.0.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:2.0.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-security:2.0.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-mail:2.0.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-webflux:2.0.5.RELEASE'
    compile 'org.modelmapper:modelmapper:2.0.0'
    compile 'com.auth0:java-jwt:3.3.0'
    compile 'com.cloudinary:cloudinary-http44:1.18.0'
    compile 'javax.xml.bind:jaxb-api:2.3.0'
    runtime 'org.postgresql:postgresql:42.2.5'
    testCompile('org.springframework.boot:spring-boot-starter-test:2.0.5.RELEASE') {
        exclude(module: 'commons-logging')
    }
    testCompile 'com.h2database:h2:1.4.196'
    testCompile 'org.springframework.security:spring-security-test:5.0.7.RELEASE'
    compileOnly 'org.projectlombok:lombok:1.16.22'
    apt "org.projectlombok:lombok:1.16.22"
}

publishing {
    publications {
        blogggr(MavenPublication) {
            artifact bootJar
            version version
        }
    }
    repositories {
        maven {
            if(project.version.endsWith('-SNAPSHOT')) {
                url "${nexus_url}maven-snapshots/"
            } else {
                url "${nexus_url}maven-releases/"
            }
            credentials {
                username nexus_username
                password nexus_password
            }
        }
    }
}

release {
    failOnCommitNeeded = false
    failOnUnversionedFiles = false
    scmAdapters = [
            net.researchgate.release.GitAdapter
    ]
    git {
        requireBranch = 'master'
    }
}

afterReleaseBuild.dependsOn publish

task getJarName {
    doLast {
        println project.name + '-' + project.version + '.jar'
    }
}

task getVersion {
    doLast {
        println version
    }
}

def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

sonarqube {
    String projectKey = project.name + "-" + gitBranch()
    String projectName = project.name + " " + gitBranch()
    properties {
        property "sonar.projectKey", projectKey
        property "sonar.projectName", projectName
    }
}

jacoco {
    toolVersion = "0.8.1"
    reportsDir = file("$buildDir/jacocoReportDir")
}